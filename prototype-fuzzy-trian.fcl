FUNCTION_BLOCK driver

VAR_INPUT
  x : REAL;
  y : REAL;
  angle : REAL;
END_VAR

VAR_OUTPUT
  volante : REAL;
END_VAR

FUZZIFY x
  TERM esquerda   := (0, 1) (0.35, 0.5) (0.5, 0);
  TERM centro     := TRIAN 0.0 0.45 1;
  TERM direita    := (0.5, 0) (0.65, 0.5) (1, 1);
END_FUZZIFY

FUZZIFY y
  TERM topo    := TRIAN 0 0 0.3;
  TERM centro  := TRIAN 0.25 0.45 0.75;
  TERM baixo   := TRIAN 0.7 1 1;
END_FUZZIFY

FUZZIFY angle
  TERM leste    := (0, 1) (2.5, 1) (90, 0) (270, 0) (357.5, 1) (360, 1);
  TERM oeste    := TRIAN 90 177.5 270;
  TERM norte    := TRIAN 0 87.5 180;
  TERM sul      := TRIAN 180 267.5 360;
  TERM nordeste := TRIAN 0 30 90;
  TERM noroeste := TRIAN 90 120 180;
  TERM sudeste  := TRIAN 270 300 360;
  TERM sudoeste := TRIAN 180 210 270;
END_FUZZIFY

DEFUZZIFY volante
  TERM esterce_esquerda := TRIAN -1 -0.8 0;
  TERM esterce_nada     := TRIAN -0.5 -0.1 0.5;
  TERM esterce_direita  := TRIAN 0 0.8 1;
  // http://jfuzzylogic.sourceforge.net/html/example_fcl.html
  // In the list of fuzzy methods there is one called center of area
  // https://en.wikipedia.org/wiki/Defuzzification
  // http://zone.ni.com/reference/en-XX/help/370401G-01/lvpid/defuzzification_methods/
  METHOD                : COA;
  DEFAULT               := 0;
END_DEFUZZIFY

RULEBLOCK No1
  // http://jfuzzylogic.sourceforge.net/html/example_fcl.html
  // Use 'min' for 'and' (also implicit use 'max'
  // for 'or' to fulfill DeMorgan's Law)
  AND : MIN;
  // Use 'min' activation method
  ACT : MIN;
  // Use 'max' accumulation method
  ACCU : MAX;

  RULE 1  : IF x IS esquerda AND angle IS leste  THEN volante IS esterce_direita;
  RULE 2  : IF x IS esquerda AND angle IS norte THEN volante IS esterce_direita;
  RULE 3  : IF x IS esquerda AND angle IS oeste  THEN volante IS esterce_nada;
  RULE 4  : IF x IS esquerda AND angle IS sul THEN volante IS esterce_esquerda;
  RULE 5  : IF x IS esquerda AND angle IS sudeste THEN volante IS esterce_esquerda;
  RULE 6  : IF x IS esquerda AND angle IS nordeste THEN volante IS esterce_direita;
  RULE 7  : IF x IS esquerda AND angle IS sudoeste THEN volante IS esterce_esquerda;
  RULE 8  : IF x IS esquerda AND angle IS noroeste THEN volante IS esterce_nada;

  RULE 9  : IF x IS centro AND angle IS leste THEN volante IS esterce_direita;
  RULE 10 : IF x IS centro AND angle IS oeste THEN volante IS esterce_esquerda;
  RULE 11 : IF x IS centro AND angle IS nordeste THEN volante IS esterce_direita;
  RULE 12 : IF x IS centro AND angle IS noroeste THEN volante IS esterce_esquerda;
  RULE 13 : IF x IS centro AND angle IS sul THEN volante IS esterce_nada;

  RULE 14 : IF x IS direita AND angle IS leste THEN volante IS esterce_nada;
  RULE 15 : IF x IS direita AND angle IS norte THEN volante IS esterce_esquerda;
  RULE 16 : IF x IS direita AND angle IS oeste THEN volante IS esterce_direita;
  RULE 17 : IF x IS direita AND angle IS sul THEN volante IS esterce_direita;

END_RULEBLOCK

END_FUNCTION_BLOCK
